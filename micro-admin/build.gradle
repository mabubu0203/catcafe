apply from: "$rootDir/gradle/node.gradle"

dependencies {

    // SpringBoot Projects
    implementation("org.springframework.boot:spring-boot-starter-actuator")
    implementation("org.springframework.boot:spring-boot-starter-thymeleaf")
    implementation("org.springframework.boot:spring-boot-starter-web")
    implementation("org.springframework.boot:spring-boot-starter-webflux")
    annotationProcessor("org.springframework.boot:spring-boot-configuration-processor")
    developmentOnly("org.springframework.boot:spring-boot-devtools")

    // springdoc-openapi-ui
    implementation("org.springdoc:springdoc-openapi-ui:${springDocVersion}")

    // lombok
    compileOnly("org.projectlombok:lombok")
    annotationProcessor("org.projectlombok:lombok")
    testImplementation("org.springframework.boot:spring-boot-starter-test") {
        exclude group: "org.junit.vintage", module: "junit-vintage-engine"
    }

}

test {
    useJUnitPlatform()
}

springBoot {
    buildInfo()
}

jar {
    archiveBaseName = "micro-admin"
    archiveVersion = "$catCafeVersion"
    enabled = true
}

bootJar {
    enabled = true
    launchScript()
}

node {
    version = "12.18.3"
    download = true
    workDir = file("${project.projectDir}/src/main/vue/.gradle/nodejs")
    npmWorkDir = file("${project.projectDir}/src/main/vue/.gradle/npm")
    nodeModulesDir = file("${project.projectDir}/src/main/vue")
}

compileJava.dependsOn webpack
compileJava.mustRunAfter webpack